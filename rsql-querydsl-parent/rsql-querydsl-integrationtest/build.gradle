buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.0.RELEASE")
    }
}

apply plugin: 'spring-boot'

springBoot {
    mainClass = "com.github.vineey.rql.querydsl.test.Application"
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "src/generated/java"]
        }
    }
}

configurations {
    querydslapt
}

description = 'Integration test for all rsql querydsl modules'

dependencies {
    compile 'com.querydsl:querydsl-core:4.1.3'
    compile "com.querydsl:querydsl-jpa:4.1.3"
    compile 'com.querydsl:querydsl-sql:4.1.3'
    compile 'com.querydsl:querydsl-mongodb:4.1.3'
    compile 'org.hibernate:hibernate-core:5.1.0.Final'
    compile 'org.hibernate:hibernate-entitymanager:5.1.0.Final'
    compile 'org.springframework.data:spring-data-mongodb:1.9.2.RELEASE'
    compile("org.springframework.boot:spring-boot-starter-data-jpa:1.4.0.RELEASE")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb:1.4.0.RELEASE")
    compile "javax.validation:validation-api:1.1.0.Final"
    compile "org.hibernate:hibernate-validator:5.2.4.Final"
    compile "org.hibernate:hibernate-java8:5.1.0.Final"
    compile project(':rsql-querydsl-parent:rsql-querydsl-all')
    compile project(':rsql-querydsl-parent:rsql-querydsl-spring')
    compile 'com.google.guava:guava:19.0'

    querydslapt 'com.querydsl:querydsl-apt:4.1.3'
    testCompile("org.springframework.boot:spring-boot-starter:1.4.0.RELEASE")
    testCompile("junit:junit")
    testCompile("org.springframework.boot:spring-boot-starter-test:1.4.0.RELEASE")
    testCompile "com.github.springtestdbunit:spring-test-dbunit:1.3.0"
    testCompile "org.dbunit:dbunit:2.5.3"
    testCompile("com.h2database:h2:1.4.192")
    testCompile "com.lordofthejars:nosqlunit-mongodb:0.10.0"

}


task generateQueryDSLMongo(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL Mongo query types') {
    source = 'src/main/java/com/github/vineey/rql/querydsl/test/mongo'
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "org.springframework.data.mongodb.repository.support.MongoAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

task generateQueryDSLJPA(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL JPA query types') {
    source = 'src/main/java/com/github/vineey/rql/querydsl/test/jpa'
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileGeneratedJava {
    dependsOn generateQueryDSLMongo, generateQueryDSLJPA
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

compileJava {
    dependsOn generateQueryDSLMongo, generateQueryDSLJPA
    source generateQueryDSLJPA.destinationDir, generateQueryDSLMongo.destinationDir
}
